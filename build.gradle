import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.0.0'
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

group 'ch.aki'
version '1.0.0'

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11


dependencies {
    implementation group: 'com.typesafe.akka', name: 'akka-stream-kafka_2.13', version: '2.0.7'
    implementation group: 'com.typesafe.akka', name: 'akka-stream_2.13', version: '2.6.14'
    implementation group: 'com.typesafe.akka', name: 'akka-slf4j_2.13', version: '2.6.14'
    implementation "ch.qos.logback:logback-classic:1.2.3"

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

jib {
    from {
        image = 'adoptopenjdk/openjdk11'
    }
    to {
        image = project.name
    }
    container {
        jvmFlags = ['-Dconfig.override_with_env_vars=true']
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'aki.AlpakkaStream'
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

}


shadowJar {
    transform(AppendingTransformer) {
        resource = 'reference.conf'
    }
    with jar
}